apply plugin: 'com.android.application'

static def gitLatestTag() {
    try {
        def cmd = 'git describe --abbrev=0 --tags'
        def result = cmd.execute().text.trim()
        return result
    } catch (Exception e) {
        return '1.0.0'
    }
}

android {
    compileSdkVersion rootProject.compileSdkVersion
    buildToolsVersion rootProject.buildToolsVersion

    defaultConfig {
        applicationId "me.yokeyword.sample"
        minSdkVersion rootProject.minSdkVersion
        targetSdkVersion rootProject.targetSdkVersion
        versionCode 1
        versionName gitLatestTag()
    }

    signingConfigs {
        releaseConfig {
            storeFile file("./fragmentation.jks")
            storePassword System.getenv("KEYSTORE_PASS")
            keyAlias System.getenv("ALIAS_NAME")
            keyPassword System.getenv("ALIAS_PASS")
        }
    }

    buildTypes {
        release {
            minifyEnabled false

            signingConfig signingConfigs.releaseConfig

            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }


    android.applicationVariants.all { variant ->
        variant.outputs.all {
            if (variant.buildType.name == 'release') {
                outputFileName = "fragmentation.apk"
            }
        }
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    testCompile 'junit:junit:4.12'
    compile "com.android.support:appcompat-v7:$rootProject.v4Version"
    compile "com.android.support:design:$rootProject.v4Version"
    compile "com.android.support:cardview-v7:$rootProject.v4Version"
    compile "com.android.support:recyclerview-v7:$rootProject.v4Version"
    compile project(':fragmentation')
    compile project(':fragmentation_swipeback')
    // EventBus
    compile project(':eventbus_activity_scope')
    compile 'org.greenrobot:eventbus:3.0.0'
    //    apt 'org.greenrobot:eventbus-annotation-processor:3.0.1'
}

